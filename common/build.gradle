import org.apache.tools.ant.filters.ReplaceTokens

architectury {
    common(rootProject.enabled_platforms.split(",")) {
        it.platformPackage 'neoforge', 'neo'
    }
}

loom {
//    forge { accessTransformer 'src/main/resources/accesstransformer.cfg' }
    accessWidenerPath.set file("src/main/resources/pangaea.accesswidener")
}

dependencies {
    // Only needed for suppressing Fabric build spam
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
    modImplementation "com.personthecat:catlib:$catlib_version"
}

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.properties(mod_version: mod_version, mod_id: mod_id, mod_name: mod_name)
    filesMatching(['**/Pangaea.java']) {
        filter(ReplaceTokens, tokens: [MOD_VERSION: mod_version, MOD_ID: mod_id, MOD_NAME: mod_name])
    }
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
    options.compilerArgs << '-parameters'
}
