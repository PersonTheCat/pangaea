
architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig 'pangaea.common.mixins.json'
    }
}

configurations {
    developmentForge.extendsFrom common
}

repositories {
    maven { url 'https://cursemaven.com' }
}

dependencies {
    shadowCommon(project(path: ':common', configuration: 'transformProductionForge'))
    common(project(path: ':common', configuration: 'namedElements'))
    forge "net.minecraftforge:forge:$mc_version-$forge_version"
    modImplementation group: 'curse.maven', name: 'configured-457570', version: configured_maven_version_forge
    modImplementation("com.personthecat:catlib-forge:$catlib_version") {
        exclude group: 'org.jetbrains', module: 'annotations'
    }
}

processResources {
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand project.properties
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set 'dev'
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
