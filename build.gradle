
plugins {
    id "architectury-plugin" version "$architectury_version"
    id "dev.architectury.loom" version "$architectury_loom_version" apply false
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
}

architectury {
    minecraft = mc_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'com.github.johnrengelman.shadow'

    loom {
        silentMojangMappingsLicense()
    }

    configurations {
        common { transitive = false }
        shadowCommon { transitive = false }
        compileClasspath.extendsFrom common
        runtimeClasspath.extendsFrom common
    }

    dependencies {
        minecraft "com.mojang:minecraft:$mc_version"
        mappings loom.officialMojangMappings()

        compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.32'

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.2'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.2'
        testImplementation group: 'org.jetbrains', name: 'annotations', version: '24.1.0'
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    archivesBaseName = "$archive_base-$platform"
    version mod_version

    repositories {
        // More convenient access to CF mods.
        maven { url = 'https://www.cursemaven.com' }
        mavenLocal()
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        ignoreFailures = false
        failFast = true
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
}
